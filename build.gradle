plugins{
    id 'com.google.cloud.tools.jib' version '3.4.5' apply false
}

def envFile = file(".env")
def envProps = new Properties()
envFile.withReader('UTF-8') { reader ->
    envProps.load(reader)
}
envProps.each { key, value ->
    project.ext.set(key, value)
}

def dockerUsername = project.hasProperty("DOCKER_USERNAME") ? project.DOCKER_USERNAME : System.getenv("DOCKER_USERNAME") ?: ""
def dockerPassword = project.hasProperty("DOCKER_PASSWORD") ? project.DOCKER_PASSWORD : System.getenv("DOCKER_PASSWORD") ?: ""

def useLocalImages = !dockerUsername

subprojects {
    if (project.path.startsWith(':services:')) {
        apply plugin: 'java'
        apply plugin: 'com.google.cloud.tools.jib'

        def imageName = useLocalImages
                ? "${project.name}-techshop:latest"
                : "docker.io/${dockerUsername}/${project.name}-techshop:latest"

        jib {
            from {
                image = "eclipse-temurin:21-jdk"
            }
            to {
                image = imageName
                auth {
                    username = dockerUsername
                    password = dockerPassword
                }
            }
        }
    }
}